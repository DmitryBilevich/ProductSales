SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[SearchOrdersWithItems]
    @FromDate DATE = NULL,
    @ToDate DATE = NULL,
    @CustomerName NVARCHAR(100) = NULL,
    @ProductIDs ProductIDList READONLY
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        o.OrderID,
        o.CustomerName,
        o.CustomerPhone,
        o.CustomerEmail,
        o.OrderDate,
        Items = (
            SELECT 
                oi.OrderItemID,
                oi.Quantity,
                oi.UnitPrice,
                p.ProductID,
                p.Name AS ProductName,
                p.Category,
                p.Price AS ProductCurrentPrice
            FROM OrderItems oi
            JOIN Products p ON p.ProductID = oi.ProductID
            WHERE oi.OrderID = o.OrderID
              AND (
                  NOT EXISTS (SELECT 1 FROM @ProductIDs)
                  OR oi.ProductID IN (SELECT ProductID FROM @ProductIDs)
              )
            FOR JSON PATH
        )
    FROM Orders o
    WHERE
        (@FromDate IS NULL OR o.OrderDate >= @FromDate) AND
        (@ToDate IS NULL OR o.OrderDate <= @ToDate) AND
        (@CustomerName IS NULL OR o.CustomerName LIKE '%' + @CustomerName + '%')
    ORDER BY o.OrderDate DESC
END
GO
